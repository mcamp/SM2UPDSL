grammar dk.sdu.mmmi.assa.sm.StateMachine with org.eclipse.xtext.common.Terminals

generate stateMachine "http://www.sdu.dk/mmmi/assa/sm/StateMachine"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Root:
	(
		'project' name=ID
		machines+=Machine*
	)?;
	
Machine:
	'machine' name=ID '{' (vars+=VarDefinition | states+=State | transitions+=Transition)* '}';
	
State:
	(fail?='fail' | end?='end')? 'state' name=ID ('safety' 'properties' '{' properties+=SafetyProperty* '}')? ('with' machine=Machine)?
;

Transition:
	from=[State] '->' to=[State]
		(hasGuard?='guard' guard=Boolean)?
		(hasWhen?='when' when=ID)?
		(time?='after' timeout=Float)?
		(hasSignal?='signal' signal=ID)?
		('actions' '{' actions+=Statement* '}')?
;

SafetyProperty: 
	{Delay} 'startup' 'delay' time=Float |
	{MaxExecutionTime} 'max' 'execution' time=Float

;

VarDefinition: 
	type=Type name=ID ':=' expression=Expression
;

Statement:
	{VarAssignation} variable=[VarDefinition] ':=' expression=Expression
;

Expression:
	{SMNumber} value=INT |
	{SMBool} value=Boolean
;

Type: 'bool';

Float returns ecore::EFloat: INT ('.' INT)?;
Boolean returns ecore::EBoolean: 'true' | 'false';
